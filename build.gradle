buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'pmd'
    alias(libs.plugins.gradle.plugin.publish)
    alias(libs.plugins.shadow)
    //alias(libs.plugins.snyk)
}

ext {
    pluginDescription = 'A template for create a gradle plugin'
}

configurations {
    plugin
    implementation.extendsFrom(plugin)

    mockitoAgent
    pmdTool

    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }
}

shadowJar {
    configurations = [project.configurations.plugin]
    archiveClassifier.set("")
    relocate 'xyz.ronella.trivial', "${project.group}.shadow.trivial"
    minimize()
}

gradlePlugin {
    website = '<UPDATE>'
    vcsUrl = '<UPDATE>'
    description = pluginDescription
    plugins {
        templateGradlePlugin {
            id = 'xyz.ronella.gradle.plugin'
            displayName = 'Template Gradle Plugin'
            description = pluginDescription
            implementationClass = 'xyz.ronella.gradle.plugin.TemplateGradlePlugin'
            tags = ['<UPDATE>']
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

/*snyk {
    arguments = '--all-sub-projects'
    severity = 'low'
    api = "${snykToken}"
    autoDownload = true
    autoUpdate = true
}*/

repositories {
    mavenCentral()
}

dependencyLocking {
    lockAllConfigurations()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    plugin libs.trivial.chunk

    testImplementation testLibs.bundles.unit.test
    mockitoAgent(testLibs.mockito.core) {
        transitive = false
    }

    pmdTool libs.pmd //This must be the only dependency attached to the pmdTool configuration.
}

test {
    jvmArgs("-javaagent:${configurations.mockitoAgent.asPath}")
    useJUnitPlatform()
    dependsOn pmdMain
    finalizedBy(jacocoTestReport)
    //finalizedBy('snyk-test')
}

jar {
    enabled = false
    dependsOn test, shadowJar
    //dependsOn('snyk-monitor')
    finalizedBy(javadocJar, sourcesJar)
}

pmd {
    consoleOutput = true
    toolVersion = configurations.pmdTool.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion.first
    rulesMinimumPriority = 5
    ruleSetFiles = files('quality/pmd/java/custom.xml')
    ruleSets = []
    logger.lifecycle("PMD version: ${pmd.toolVersion}")
}

pmdTest.enabled = false

shadowJar.mustRunAfter(test)
